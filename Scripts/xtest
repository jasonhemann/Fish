#! /bin/sh
#| -*- racket -*-
exec racket -tm "$0" -- ${1+"$@"}
|#
#lang racket

;; usage:
;; ./xtest path-to-player --file path-to-file.json
;; or
;; ./xtest path-to-player players = P rows = R ...
;; 
;; CONSTRAINTS
;; `path-to-player` is expected to consume the port as the first command-line argument. 
;; The configuration must include the number of house players P that compete against `path-to-player`.

;; Configures the `server` to set up some number P of (internal) house players that compete
;; against the specified external player `path-to-player` running on the same machine but
;; communicating via TCP with the `server`.
;; 
;; The script forwards the remote player's STDOUT but only for up to 3s after the tournament is over.

;; EXAMPLE:
;;   ./xtest ./x1client players = 4
;; This sets up four house players and starts `x1client port` as an external process.
;; `x1client` sets up a standard, greedy player that displays each (remote) method call to STDOUT.

;; TODO:
;; -- include the specification for an internal observer

(provide main)

;; ---------------------------------------------------------------------------------------------------
(require Fish/Scripts/Private/configure)
(require Fish/Scripts/Private/run-server-client)
(require Fish/Player/player)
(require Fish/Player/greedy)

(require SwDev/Testing/harness)
(require SwDev/Testing/communication)

;; ---------------------------------------------------------------------------------------------------

(define TIME (+ (+ 30 30) 60)) ;; maximum wait time for tournament to end; should probably be inf

(define (main path-to . c)
  (define h (configure-server c))
  (define players (build-list (dict-ref h 'players) (λ _ (new player% [strategy (new strategy%)]))))
  (define-values (s-in _1 tear-down-server) (local-setup (λ () (run-server h #:house players))))
  (define-values (r-in _2 tear-down-remote) [(make-setup path-to `(,(~a (dict-ref h 'port))) values)])

  (define th
    (thread
     (λ ()
       (let loop ()
         (define next (read-line r-in))
         (unless (eof-object? next)
           (displayln next)
           (loop))))))

  ;; after the tournament is run: 
  (parameterize ((io-time-out TIME))
    (displayln `[the result produced by the server ,(read-message s-in)]))
  
  (tear-down-server)

  (sync/timeout 3 th)
  (tear-down-remote))
